plugins {
    id("java-library")
    id("xyz.jpenilla.run-paper") version "3.0.2"
    id("de.eldoria.plugin-yml.paper") version "0.8.0"
}

final String VERSION = "1.3.0"
final String RUN_NUMBER = System.getenv("GITHUB_RUN_NUMBER") ?: "DEV"

group = "cloud.grabsky"
version = "$VERSION+$RUN_NUMBER"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
}

dependencies {
    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.42")
    annotationProcessor("org.projectlombok:lombok:1.18.42")
    // Paper API
    compileOnly("io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT")
    // Lamp (Commands)
    paperLibrary("io.github.revxrsal:lamp.common:4.0.0-rc.14")
    paperLibrary("io.github.revxrsal:lamp.bukkit:4.0.0-rc.14")
    paperLibrary("io.github.revxrsal:lamp.brigadier:4.0.0-rc.14")
    // Spec (Configuration)
    paperLibrary("io.github.revxrsal:spec:1.5")
}

tasks {
    runServer {
        minecraftVersion("1.21.11")
    }
    compileJava {
        options.fork = true
        options.compilerArgs += "-parameters"
        // Setting compatibility to Java 21 and higher.
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
}

paper {
    main = "cloud.grabsky.recipes.Recipes"
    loader = "cloud.grabsky.recipes.Recipes\$PluginLoader"
    apiVersion = "1.21.1"
    foliaSupported = true
    generateLibrariesJson = true
    authors = ["LoreSchaeffer", "Grabsky"]
}

// Configuring runServer task to use JetBrains' JDK 21 for expanded hot-swap features.
tasks.withType(xyz.jpenilla.runtask.task.AbstractRun.class).configureEach {
    javaLauncher = javaToolchains.launcherFor {
        vendor = JvmVendorSpec.JETBRAINS
        languageVersion = JavaLanguageVersion.of(21)
    }
    jvmArgs("-Xmx1G", "-XX:+AllowEnhancedClassRedefinition", "-Dcom.mojang.eula.agree=true", "-Dnet.kyori.ansi.colorLevel=truecolor")
}

// Returns formatted release name. (for GitHub Actions)
tasks.register("getRelease", () -> print("$VERSION #$RUN_NUMBER"))

// Returns formatted tag name. (for GitHub Actions)
tasks.register("getTag", () -> print("$VERSION+$RUN_NUMBER"))


